apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.6'
    }
}

repositories {
    mavenCentral()
    jcenter()
}


dependencies {
    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-core:2.7.19")
    //I don't think this is necessary but if something breaks with tests down the line, probably should have it in
    //testCompile("com.lemmingapex.trilateration:trilateration:1.0.2")
    compile("com.lemmingapex.trilateration:trilateration:1.0.2")
    compile("com.google.code.gson:gson:2.8.2")
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

task createDockerfile(type: Dockerfile) {
    destFile = project.file("build/docker/Dockerfile")
    from "tomcat:9-jre8"
    //copyFile("tomcatconf/tomcat-users.xml", "/usr/local/tomcat/conf/")
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "rocate/tomcat:latest"
}

task createDockerContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    targetImageId {buildDockerImage.getImageId()}
    portBindings = ['8888:8080']
}

task dockerDeploy(type: DockerCopyFileToContainer) {
    dependsOn = [war, createDockerContainer]
    targetContainerId {createDockerContainer.getContainerId()}
    hostPath = project.file("build/libs/${rootProject.name}.war")
    remotePath = "/usr/local/tomcat/webapps/"
}

task startDockerContainer(type: DockerStartContainer) {
    dependsOn = [dockerDeploy, createDockerContainer]
    targetContainerId {createDockerContainer.getContainerId()}

    doLast {
        project.file("build/docker/runningContainerId.txt").text = """${createDockerContainer.getContainerId()}"""
    }
}

task stopDockerContainer(type: DockerStopContainer) {
    targetContainerId { project.file("build/docker/runningContainerId.txt").text }
    finalizedBy {internalContainerCleanup}
}

task internalContainerCleanup(type: DockerRemoveContainer) {
    targetContainerId { project.file("build/docker/runningContainerId.txt").text }
    finalizedBy {internalDeleteContainerId}
}

task internalDeleteContainerId(type: Delete) {
    delete "build/docker/runningContainerId.txt"
}
